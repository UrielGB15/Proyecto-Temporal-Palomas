\  PROGRAMA DE AUTOMOLDAMIENTO - EXP TEMPORAL
\  MOLDEAMIENTO A TECLA DERECHA-ROJA
\  VERSION 2017 JUNIO  PALOMAS LAB25 COMP-ADAPT 

\INPUTS
^TecCen = 1 \Tecla Central
^TecIzq = 2 \Tecla Izquierda
^TecDer	=3 \Tecla Derecha

\OUTPUTS
^LDR      = 10  \Luz Derecha Roja
^LG      = 14  \Luz General
^COMEDERO = 12 \Entrega de Comida

 \  VARIABLES USADAS EN EL PROGRAMA
 \  A = Tasa Rta TecIzq 
 \  B = Tasa Rta TecDer
 \  C = Tasa Rta TecCen
 \  E = Contador de Reforzadores
 \  F = Valor de blackouts disponibles
 \  T = Tiempo Sesion en Minutos no Reseteable
 \  G = Lista seleccionar duraciones Blackout
 \  Z = ARREGLO EN TIEMPO REAL


\ LISTA PARA INTERVALO ENTRE ENSAYOS
LIST F = 90,40,85,55,30

\  DIM USED IN THIS PROGRAM
DIM Z = 49999

\ XX.11 = Picar Tecla Derecha cuando Roja
\ XX.12 = Picar Tecla Derecha cuando Blackout
\ XX.21 = Picar Tecla Central cuando Roja
\ XX.22 = Picar Tecla Central Cuando Blackout
\ XX.31 = Picar Tecla Izquierda cuando Roja
\ XX.32 = Picar Tecla Izquierda Cuando Blackout
\ XX.4  = Entrega del Reforzador

\   ****************************************************
\                            SHOW
\   ****************************************************
S.S.1,

S1,
  #START--->S2
S2,
	0.1": SHOW 1, TSESION, T;
	SHOW 2, TecDer, B;
	SHOW 3, TecCen, A;
	SHOW 4, TecIzq, C;
	SHOW 6, REFOR, E;
        SHOW 5, TBLACKO,G ---> SX

\   ****************************************************
\                         TIEMPO
\   ****************************************************
S.S.2, 
\Tiempo de SesiÃ³n en Minutos.
S1,
  #START:--->S2
S2,
  1": ADD T--->SX

\\\\\\\\ PARA FINALIZAR SESION POR TIEMPO \\\\\\\\\\\\\\
S.S.3,
S1,
	#START: ---> S2
S2,
	90': OFF^COMEDERO; OFF^LDR; 
	OFF^LG ---> STOPABORTFLUSH

\\\\\\\\\ PARA FINALIZAR SESION POR REFORZADOR \\\\\\\\\\\\\
S.S.4,
S1,
	#START: ---> S2
S2,
	0.01": IF E=75 [@V, @F]
	@V: OFF^COMEDERO; OFF^LDR; 
		OFF^LG ---> STOPABORTFLUSH
	@F: ---> SX

\   ****************************************************
\                         AUTOMOLDAMIENTO
\   ****************************************************

\\\\\\\\\\\\\\\\\\\\\\\\Encendido Tecla Roja
S.S.5,
S1, 
	#START:---> S2

\ Escogemos un valor con reemplazo para el tiempo blackout
S2,
	0.01": ON^LG; RANDI G = F ---> S3
s3,
	0.01": ON^LDR; SET G = G*1"---> S4

\ La telca Roja se encendera por 8seg, despues reforzador
\ Registramos todas las posibles respuestas en teclas
S4,
	8": OFF^LDR ---> S5
	#R^TecDer: ADD B;
	SET Z(B-1) = T+.11 --->SX
	#R^TecCen: ADD A;
	SET Z(A-1) = T+.21 ---> SX
	#R^TecIzq: ADD C;
	SET Z(C-1) = T+.31 ---> SX
S5,
	0.01": ON^COMEDERO; OFF^LG ---> S6
S6,
	2": OFF^COMEDERO; ADD E;
        SET Z(E-1)=T+.4 ---> S7

\ Registramos todas las posibles respuestas en teclas
S7,
	G#T:--->S2 \BLACKOUT ENTRE DURACIONES
	#R^TecDer: ADD B;
	SET Z(B-1) = T+.12 --->SX
	#R^TecCen: ADD A;
	SET Z(A-1) = T+.22 ---> SX
	#R^TecIzq: ADD C;
	SET Z(C-1) = T+.3 ---> SX
